#include <WinAPI.au3>
#include <Constants.au3>

; Constants (see also: common.h)
Global Const $GTA2_PLAYER_COUNT = 6
Global Const $GTA2_LOBBY_CTRL_START = 1021
Global Const $GTA2_LOBBY_CTRL_LIST  = 1024


; Global variables
Global $global_sock


; This gets generated by SDL2 in the menu and has a trailing slash.
; The menu sends this as soon, as the meta component connects.
Global $global_config_path = NULL

Global $global_game_instances_open = 0
Global $global_game_process_ids[$GTA2_PLAYER_COUNT]
Global $global_game_screen_layouts[$GTA2_PLAYER_COUNT]

; Initialize Arrayss
For $i=0 To $GTA2_PLAYER_COUNT-1
	$global_game_process_ids[$i] = 0
Next

; Zero-terminated string reply to the server
Func re($message)
	TCPSend($global_sock, $message & Chr(0))
Endfunc


Func send_pid_table()
	Local $str = "PID_TABLE"
	For $i=0 To $GTA2_PLAYER_COUNT - 1
		$str &= " " & $global_game_process_ids[$i]
	Next
	re($str)
Endfunc


; Return values:
; 		$ret[0]: count of hwnds
; 		$ret[1]: first hwnd
; 		...
Func get_all_hwnds_from_pid($pid)
	Local $list = WinList()
	Local $ret[1]
	$ret[0] = 0
	
	For $i = 1 To $list[0][0]
	
		Local $hwnd = $list[$i][1]
		
		If WinGetProcess($hwnd) == $pid Then
			_ArrayAdd($ret, $hwnd)
			$ret[0] += 1
		Endif
	Next
	
	Return $ret
Endfunc

Func find_hwnd_with_control($hwnds, $ctrl_id)
	For $i=1 To $hwnds[0]
		If ControlCommand($hwnds[$i], "", $ctrl_id, "IsVisible") Then
			Return $hwnds[$i]
		Endif
	Next
	
	Return Null
Endfunc

Func wait_for_hwnd_with_control($pid, $ctrl_id)
	While True
		Local $hwnd = find_hwnd_with_control( _
			get_all_hwnds_from_pid($pid), $ctrl_id)
		If $hwnd Then _
			Return $hwnd
		
		Sleep(100)
	Wend
Endfunc

; Returns the single hwnd id that is left
Func wait_until_only_one_hwnd_left($pid)
	While True
		Local $hwnds = get_all_hwnds_from_pid($pid)
		
		If IsArray($hwnds) And $hwnds[0] == 1 Then _
			Return $hwnds[1]
		
		Sleep(100)
	Wend
Endfunc

Func wait_for_listview_entry_count($hwnd, $ctrl_id, $count)
	While True
		Local $current = ControlListView($hwnd, "", $ctrl_id, _
			"GetItemCount")
		
		If $current == $count Then _
			Return
		
		Sleep(100)
	Wend
Endfunc


Func move_until_it_works($hwnd, $geo)
	While True
		_WinAPI_SetWindowPos($hwnd, $HWND_TOP, $geo[0], $geo[1], _
			$geo[2], $geo[3], $SWP_NOSIZE)
		
		Local $pos = WinGetPos($hwnd)
		If $pos[0] == $geo[0] And $pos[1] == $geo[1] Then _
			Return
			
		re("moving window " & $hwnd & " failed, tying again...")
		Sleep(100)
	Wend
Endfunc

Func regwrite_if_empty($keyname, $valuename, $type, $value)
	RegRead($keyname, $valuename)
	If @Error Then _
		RegWrite($keyname, $valuename, $type, $value)
EndFunc




