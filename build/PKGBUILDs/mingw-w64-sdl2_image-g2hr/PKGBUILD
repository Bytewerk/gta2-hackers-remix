# based on the AUR package
# https://aur.archlinux.org/packages/mingw-w64-sdl2_image/
#
# changes:
# - i686 only
# - static only
# - only build with TGA and (statically linked) PNG support
#

pkgname=mingw-w64-sdl2_image-g2hr
pkgver=2.0.0
pkgrel=1
conflicts=("mingw-w64-sdl2_image")
pkgdesc="SDL2 image libraries (mingw-w64)"
arch=(any)
url="http://www.libsdl.org/projects/SDL_image/"
license=("zlib")
depends=(mingw-w64-crt mingw-w64-sdl2 mingw-w64-libpng)
makedepends=(mingw-w64-configure)
options=(staticlibs !strip !buildflags)
source=("http://www.libsdl.org/projects/SDL_image/release/SDL2_image-$pkgver.tar.gz")
md5sums=('fb399c8386fb3248f1b33cfe81bdf92b')

_arch="i686-w64-mingw32"

build() {
	cd "${srcdir}/SDL2_image-${pkgver}"
	mkdir -p build-${_arch} && pushd build-${_arch}
	${_arch}-configure \
		--disable-shared --enable-static \
		--disable-sdltest \
		--disable-bmp --disable-gif --disable-jpg \
		--disable-jpg-shared --disable-lbm --disable-pcx \
		--enable-png --disable-png-shared --disable-pnm \
		--enable-tga --disable-tif --disable-tif-shared \
		--disable-xcf --disable-xv --disable-webp --disable-webp-shared
	make
}

package() {
  cd "${srcdir}/SDL2_image-${pkgver}/build-${_arch}"
  make DESTDIR="$pkgdir" install
  ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
}
